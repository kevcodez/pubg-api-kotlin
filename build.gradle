buildscript {

    ext.kotlin_version = "1.2.41"

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.1.1"
        classpath "com.bmuschko:gradle-nexus-plugin:2.3.1"
    }

}

plugins {
    id 'io.codearte.nexus-staging' version '0.11.0'
}

nexusStaging {
    packageGroup = "de.kevcodez.pubg"
}

ext {
    // compile
    jackson_version = "2.9.5"
    okhttp_version = "3.10.0"
    slf4j_version = "1.7.25"

    // test
    junit_version = "5.2.0"
    mockito_kotlin_version = "1.5.0"
}

apply plugin: 'idea'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'kotlin'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'io.codearte.nexus-staging'

version = "0.1.1"
archivesBaseName = 'pubg-api-wrapper'
group = 'de.kevcodez.pubg'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
    compile "com.squareup.okhttp3:okhttp:$okhttp_version"
    compile "org.slf4j:slf4j-api:$slf4j_version"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    testCompile "com.nhaarman:mockito-kotlin:$mockito_kotlin_version"
    testCompile "org.slf4j:slf4j-simple:$slf4j_version"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

test {
    useJUnitPlatform()
}

modifyPom {
    project {
        name 'PUBG-API-Wrapper'
        description 'Teaching how to use gradle.'
        url 'https://github.com/kevcodez/pubg-api-kotlin'
        inceptionYear '2018'
        scm {
            url 'https://github.com/kevcodez/pubg-api-kotlin'
            connection 'scm:https://github.com/kevcodez/pubg-api-kotlin.git'
            developerConnection 'scm:git://github.com/kevcodez/pubg-api-kotlin.git'
        }
        licenses {
            license {
                name 'MIT License'
                url 'https://github.com/kevcodez/pubg-api-kotlin/blob/master/LICENSE'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'kevcodez'
                name 'Kevin Gr√ºneberg'
                email 'k.grueneberg1994@gmailcom'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = false
    javadoc = true
}

nexus {
    sign = true
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
}
