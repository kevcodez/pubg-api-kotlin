buildscript {

    ext.kotlin_version = "1.3.20"
    ext.dokka_version = "0.9.17"

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }

}

plugins {
    id "com.github.ben-manes.versions" version "0.20.0"
}

ext {
    // compile
    jackson_version = "2.9.8"
    okhttp_version = "3.12.1"
    slf4j_version = "1.7.25"

    // test
    junit_version = "5.3.2"
    mockk_version = "1.9"
}

apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'signing'

version = "1.0.0"
archivesBaseName = 'pubg-api-wrapper'
group = 'de.kevcodez.pubg'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
    compile "com.squareup.okhttp3:okhttp:$okhttp_version"
    compile "org.slf4j:slf4j-api:$slf4j_version"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    testCompile "org.slf4j:slf4j-simple:$slf4j_version"
    testImplementation "io.mockk:mockk:$mockk_version"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: project.javadoc) {
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }

            pom {
                name = 'PUBG-API-Wrapper'
                description = 'Playerunknowns Battlegrounds API wrapper written in Kotlin. Requires Java 1.8+'
                url = 'https://github.com/kevcodez/pubg-api-kotlin'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/kevcodez/pubg-api-kotlin/blob/master/LICENSE'
                    }
                }
                scm {
                    url = 'https://github.com/kevcodez/pubg-api-kotlin'
                    connection = 'scm:https://github.com/kevcodez/pubg-api-kotlin.git'
                    developerConnection = 'scm:git://github.com/kevcodez/pubg-api-kotlin.git'
                }
                developers {
                    developer {
                        id = 'kevcodez'
                        name = 'Kevin Gr√ºneberg'
                        email = 'k.grueneberg1994@gmail.com'
                    }
                }
            }


        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username nexusUsername
                password nexusPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
